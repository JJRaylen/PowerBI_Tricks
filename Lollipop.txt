~Sparkline TSAT_Chat_CSC = 
// Static line color - use %23 instead of # for Firefox compatibility (Measure Derived from Eldersveld Modified by Kolosko)
VAR LineColour = "%23118DFF"
VAR PointColour = "white"

// "Date" field used in this example along the X axis
VAR XMinDate = MIN(Dim_Date[Date])
VAR XMaxDate = MAX(Dim_Date[Date])

// Obtain overall min and overall max measure values when evaluated for each date
VAR YMinValue = MINX(Values(Dim_Date[Date]),CALCULATE([%TSAT_CSC_Chat]))
VAR YMaxValue = MAXX(Values(Dim_Date[Date]),CALCULATE([%TSAT_CSC_Chat]))

// Build table of X & Y coordinates and fit to 50 x 150 viewbox
VAR SparklineTable = ADDCOLUMNS(
    SUMMARIZE('Dim_Date',Dim_Date[Date]),
        "X",INT(150 * DIVIDE(Dim_Date[Date] - XMinDate, XMaxDate - XMinDate)),
        "Y",INT(50 * DIVIDE([%TSAT_CSC_Chat] - YMinValue,YMaxValue - YMinValue)))

// Concatenate X & Y coordinates to build the sparkline
VAR Lines = CONCATENATEX(SparklineTable,[X] & "," & 50-[Y]," ", Dim_Date[Date])
VAR PointTable=
    ADDCOLUMNS(
        SUMMARIZE('Dim_Date',Dim_Date[Date]),
            "@Colour",IF([%TSAT_CSC_Chat]=YMaxValue,"Green",IF([%TSAT_CSC_Chat]=YMinValue,"Red",LineColour)),
        "@Points", "<circle cx='"&INT(150 * DIVIDE(Dim_Date[Date] - XMinDate, XMaxDate - XMinDate))&"' cy='" & 50 -  INT(50 * DIVIDE([%TSAT_CSC_Chat] - YMinValue,YMaxValue - YMinValue)) & "' r='2' stroke='"&LineColour&"' stroke-width='2'  fill='"&LineColour&"'/>")

// Last data points on the line
VAR LastSparkYValue = MAXX( FILTER(SparklineTable, Dim_Date[Date] = XMaxDate), [Y])
VAR LastSparkXValue = MAXX( FILTER(SparklineTable, Dim_Date[Date] = XMaxDate), [X])

// Add to SVG, and verify Data Category is set to Image URL for this measure
VAR SVGImageURL = 
    "data:image/svg+xml;utf8," & 
    --- gradient---
    "<svg xmlns='http://www.w3.org/2000/svg' x='0px' y='0px' viewBox='-7 -7 164 64'>" &
    --- Lines---
    "<polyline 
        fill='transparent' stroke='" & LineColour & "' 
        stroke-linecap='round' stroke-linejoin='round' 
        stroke-width='1' points=' " & Lines & 
      " '/>" &
    --- Last Point---
        CONCATENATEX(PointTable,[@Points]) &
        "<circle cx='"& LastSparkXValue & "' cy='" & 50 - LastSparkYValue & "' r='2' stroke='" & LineColour & "' stroke-width='2' fill='" & PointColour & "' />" &
        "</svg>"
        
RETURN SVGImageURL
